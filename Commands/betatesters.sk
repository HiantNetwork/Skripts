options:
	prefix: &9&lBETATESTER

command betatester [<text>] [<offlineplayer>]:
	permission: skript.betatester.use 
	trigger:
		if arg-1 is "add":
			if arg-2 is not set:
				send "{@prefix} &8| &cPlease set a players name in arg 2"
				stop 
			add arg-2's uuid to {betatesters::list::*}
			send "{@prefix} &8| &aAdded &6%arg-2%&a to the beta testers whitelist"
		else if arg-1 is "remove":
			if arg-2 is not set:
				send "{@prefix} &8| &cPlease set a players name in arg 2"
				stop 
			remove arg-2's uuid from {betatesters::list::*}
			send "{@prefix} &8| &aRemoved &6%arg-2%&a from the beta testers whitelist"
		else if arg-1 is "list":
			set {_temp::1} to text component of "{@prefix} &8| &aList of BetaTesters%newline%"
			set {_i} to 1
			loop {betatesters::list::*}:
				add 1 to {_i}
				set {_p} to (loop-value parsed as an offlineplayer)
				set {_temp::%{_i}%} to text component of "&a%{_p}'s name% "
			send component {_temp::*}
		else if arg-1 is "whitelist":
			loop {betatesters::list::*}:
				add (loop-value parsed as offline player) to whitelist 
			send "{@prefix} &8| &aAdded all betatesters to the whitelist"
		else if arg-1 is "unwhitelist":
			loop {betatesters::list::*}:
				remove (loop-value parsed as offline player) from whitelist 
			send "{@prefix} &8| &aRemoved all betatesters from the whitelist"
		else if arg-1 is "kickall":
			loop {betatesters::list::*}:
				kick (loop-value parsed as offline player) due to "&bAll Betatesters Kicked"
			send "{@prefix} &8| &aKicked all beta testers"

on tab complete:
	if event-string is "/betatester":
		set tab completions for position 1 to "add", "remove", "list", "whitelist", "unwhitelist", "kickall"